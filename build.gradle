plugins {
  id 'org.springframework.boot' version "${springBootPluginVersion}" apply false
  id 'io.spring.dependency-management' version "${springDependencyManagementPluginVersion}" apply false
}

subprojects {
  group = 'dev.amir'
  version = '1.0.0'

  apply plugin: 'java'
  apply plugin: 'org.springframework.boot'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'checkstyle'
  apply plugin: 'jacoco'

  java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
  }

  jar {
    enabled = false
  }

  checkstyle {
    toolVersion = "${checkstyleVersion}"
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
    ignoreFailures = false
    maxWarnings = checkstyleMaxWarn.toInteger()
    maxErrors = checkstyleMaxErr.toInteger()
  }

  jacoco {
    toolVersion = "${jacocoVersion}"
  }

  jacocoTestReport {
    description = "Generate Jacoco coverage reports for the test build."
    reports {
      html.required.set(true)
      xml.required.set(true)
    }
  }

  test {
    useJUnitPlatform()
    filter.includePatterns = ['*Test']
    filter.excludePatterns = ['*It']
  }

  tasks.register('integrationTest', Test) {
    useJUnitPlatform()
    filter.includePatterns = ['*It']
  }
}

allprojects {
  repositories {
    mavenCentral()
    mavenLocal()
  }
}
